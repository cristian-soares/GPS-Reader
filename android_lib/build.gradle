plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.example.android_lib'
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId = 'com.example.android_lib'
            artifactId = 'android_lib'
            version = '1.0.0'

            artifact("$buildDir/outputs/aar/android_lib-release.aar")

            pom {
                name.set("Android Library")
                description.set("The Android library used in this project")
                url.set("https://github.com/cristian-soares/GPS-Reader")
                licenses {
                    license {
                        name.set("The Apache License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }
                developers {
                    developer {
                        id.set("cristian-soares")
                        name.set("Cristian Soares")
                        email.set("cmatheussoares@gmail.com")
                    }
                }
                scm {
                    connection.set("scm:git:git://github.com/cristian-soares/GPS-Reader.git")
                    developerConnection.set("scm:git:ssh://github.com/cristian-soares/GPS-Reader.git")
                    url.set("https://github.com/cristian-soares/GPS-Reader")
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cristian-soares/GPS-Reader")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("GPR_TOKEN")
            }
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}